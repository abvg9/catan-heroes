
plugins {
    id 'com.diffplug.gradle.spotless' version '3.28.0'
    id 'eclipse'
    id 'jacoco'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.notaphplover.catan-core'
version '0.4.0'

sourceCompatibility = 1.11

repositories {
    mavenCentral()
}

dependencies {
    implementation('com.diffplug.spotless:spotless-plugin-gradle:3.28.0')
    testImplementation('org.mockito:mockito-core:3.3.3')
    testImplementation('org.junit.jupiter:junit-jupiter:5.5.2')
    testImplementation('org.junit.platform:junit-platform-commons:1.6.2')
}

jacoco {
    toolVersion = "0.8.4"
    reportsDir = file("$buildDir/reports/jacoco")
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/jacoco/jacocoHtml")
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.github.notaphplover'
            artifactId = 'catan-core'
            version = '0.4.0'

            from components.java

            pom {
                name = 'Catan core'
                description = 'Core library of the game'
                url = 'https://github.com/notaphplover/catan-heroes'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'notaphplover'
                        name = 'Roberto Pintos LÃ³pez'
                        email = 'roberto.pintos.lopez@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/notaphplover/catan-heroes.git'
                    developerConnection = 'scm:git:https://github.com/notaphplover/catan-heroes.git'
                    url = 'https://github.com/notaphplover/catan-heroes/'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

sourceSets {
    main {
        java {
            srcDirs = ["src"]
        }
    }
    test {
        java {
            srcDirs = ["test"]
        }
    }
}

spotless {
  java {
    googleJavaFormat()
  }
}

test {

    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
